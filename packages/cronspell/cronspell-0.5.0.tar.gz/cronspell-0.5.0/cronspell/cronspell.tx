DateMaths:
  anchor=Anchor? formula=Formula?
;

Anchor:
    tznow=TimezoneNow | Now | isodate=ISODate
;


ISODate:
    /\d{4}-\d{2}-\d{2}\S*/
;

Now:
  'now'
;

TimezoneNow:
  Now '[' tz=/\s*([^\]]+)\s*/ ']'
;


DateMatSet:
    '{' set*=DateMath[','] '}'
;

DateMath:
    date_math_term*=DateMathTerm
;

DateMathTerm:
    statement=Statement
;

Formula:
    DateMath | DateMatSet
;

Statement:
    Floor |  Plus | Minus | CwModulo | YModulo | MModulo
;

CwModulo:
   CalendarWeekModulo value=INT?
;

YModulo:
   YearModulo value=INT?
;

MModulo:
   MonthModulo value=INT?
;

CalendarWeekModulo:
    /(%|@)\s*(CW|Cw|cw)/
;

YearModulo:
    /(%|@)\s*(Y|y)(ears?)?/
;

MonthModulo:
    /(%|@)\s*([Mm])(onths?)?/
;

Floor:
  '/' res=Clampable
;

Plus:
  '+' steps=INT? res=TimeUnitShort
;

Minus:
  '-' steps=INT? res=TimeUnitShort
;

Clampable:
    WeekDay | MonthName | TimeUnitShort | VariableTimeUnit
;

TimeUnitShort:
    W=W | d=d | M=M | H=H | S=S
;

VariableTimeUnit:
    Y=Y | m=m
;

WeekDay:
    Mon=Mon | Tue=Tue | Wed=Wed | Thu=Thu | Fri=Fri | Sat=Sat | Sun=Sun
;


MonthName:
    Jan=Jan | Feb=Feb | Mar=Mar | Apr=Apr | May=May | Jun=Jun | Jul=Jul | Aug=Aug | Sep=Sep | Oct=Oct | Nov=Nov | Dec=Dec
;

Y:
    /([Yy]ears?|Y\b)/
;

m:
    /(?!mon\b)([mM]onths?|m\b)/
;

W:
    /\b([wW]eeks?|W)\b/
;

d:
    /([dD]ays?|d\b)/
;

M:
    /([mM]inutes?|M\b|[mM]in\b)/
;

H:
    /([hH]ours?|H\b|[Hh]rs)/
;

S:
    /(?!sat$)([sS]econds?|S\b|[sS]ec\b)/
;

Mon:
    /\b[mM]on(?:day)?\b/
;

Tue:
    /\b[tT]ue(?:sday)?\b/
;

Wed:
    /\b[wW]ed(?:nesday)?\b/
;

Thu:
    /\b[tT]hu(?:rsday)?\b/
;

Fri:
    /\b[fF]ri(?:day)?\b/
;

Sat:
    /\b[sS]at(?:urday)?\b/
;

Sun:
    /\b[sS]un(?:day)?\b/
;

// ============ month names as in calendar.month_abbr ============

Jan:
    /\b[jJ]an(?:uary)?\b/
;


Feb:
    /\b[fF]eb(?:ruary)?\b/
;


Mar:
    /\b[mM]ar(?:ch)?\b/
;


Apr:
    /\b[aA]pr(?:il)?\b/
;


May:
    /\b[mM]ay\b/
;


Jun:
    /\b[jJ]une?\b/
;


Jul:
    /\b[jJ]uly?\b/
;


Aug:
    /\b[aA]ug(?:ust)?\b/
;


Sep:
    /\b[sS]ep(?:tember)?\b/
;


Oct:
    /\b[oO]ct(?:ober)?\b/
;


Nov:
    /\b[nN]ov(?:ember)?\b/
;


Dec:
    /\b[dD]ec(?:ember)?\b/
;

// //////////////////////////////////////////////
Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;
